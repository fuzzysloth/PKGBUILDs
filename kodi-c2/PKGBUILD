buildarch=8

pkgbase=kodi-c2
pkgname=('kodi-c2' 'kodi-c2-eventclients' 'kodi-c2-tools-texturepacker' 'kodi-c2-dev')
pkgver=17.1
_codename=Krypton
_commit=2e6819ea952d2abbc73114d00653a64426a52506
pkgrel=2
arch=('aarch64')
url="http://kodi.tv"
license=('GPL2')
makedepends=(
  'afpfs-ng' 'aml-libs-c2' 'bluez-libs' 'boost' 'cmake' 'curl' 'cwiid' 'doxygen'
  'git' 'glew' 'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libaacs'
  'libass' 'libbluray' 'libcdio' 'libcec' 'libcrossguid' 'libmariadbclient'
  'libmicrohttpd' 'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libssh'
  'libva' 'libvdpau' 'libxslt' 'lzo' 'mesa' 'nasm' 'nss-mdns' 'odroid-c2-libgl-fb'
  'odroid-c2-libgl-headers' 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  'rtmpdump' 'sdl2' 'sdl2_image' 'shairplay' 'smbclient' 'swig' 'taglib' 'tinyxml'
  'unzip' 'upower' 'yajl' 'zip'
)
source=(
  "${pkgname}-${pkgver}-${_codename}.tar.gz::https://github.com/Owersun/xbmc/archive/${_commit}.tar.gz"
  'kodi_permissions.conf'
  'kodi.service'
  'polkit.rules'
  '99-odroid.rules'
)
sha256sums=(
  '27a0590e1c824ce22d944126c0691be8f6152fc57c08c9ec5c4bf64da9f44191'
  '8c606f29aa9ec90f4c93e1751cfd4000872937e604e6ad7538b96ba29612d2f6'
  'f9156e33db339dc123c43ea8f7e1d8f69931efb269348d7d182cc9c93ea0fe15'
  'c68ed2bd377f80b606b8815d78239b9132b479eafc1d19797cee5824debe1800'
  '5ddf80329c9f5d054525b45f788b3405d199bfc6cf5b08c543ad29766ec27f6e'
)

prepare() {
  [[ -d kodi-build ]] && rm -rf kodi-build
  mkdir kodi-build
}

build() {
  cd kodi-build
  cmake                             \
    -DCMAKE_INSTALL_PREFIX=/usr     \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_AML=ON                 \
    -DENABLE_EVENTCLIENTS=ON        \
    -DENABLE_OPENGL=OFF             \
    -DENABLE_OPENGLES=ON            \
    -DENABLE_PULSEAUDIO=OFF         \
    -DENABLE_VAAPI=OFF              \
    -DENABLE_VDPAU=OFF              \
    -DENABLE_X11=OFF                \
    -DLIRC_DEVICE=/run/lirc/lircd   \
    ../"xbmc-${_commit}"/project/cmake
  make
  make preinstall
}

# kodi-c2
# components: kodi, kodi-bin

package_kodi-c2() {
  pkgdesc="A software media player and entertainment hub for digital media (ODROID-C2)"
  depends=(
    'bluez-libs' 'desktop-file-utils' 'fribidi' 'freetype2' 'glew'
    'hicolor-icon-theme' 'libcdio' 'libjpeg-turbo' 'libmariadbclient'
    'libmicrohttpd' 'libpulse' 'libssh' 'libva' 'libvdpau' 'libxrandr' 'libxslt'
    'lzo' 'mesa' 'python2-pillow' 'python2-simplejson' 'smbclient' 'taglib'
    'tinyxml' 'yajl' 'xorg-xdpyinfo'
  )
  optdepends=(
    'afpfs-ng: Apple shares support'
    'bluez: Blutooth support'
    'python2-pybluez: Bluetooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'libcec: Pulse-Eight USB-CEC adapter support'
    'lirc: Remote controller support'
    'lsb-release: log distro information in crashlog'
    'pulseaudio: PulseAudio support'
    'shairplay: AirPlay support'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level'
  )
  install="kodi.install"
  provides=('xbmc' 'kodi')
  conflicts=('xbmc' 'kodi')
  replaces=('xbmc')

  _components=(
  'kodi'
  'kodi-bin'
  )

  cd kodi-build
  # install eventclients
  for _cmp in ${_components[@]}; do
  DESTDIR="${pkgdir}" /usr/bin/cmake \
    -DCMAKE_INSTALL_COMPONENT="${_cmp}" \
    -P cmake_install.cmake
  done

  # Licenses
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  for licensef in LICENSE.GPL copying.txt; do
    mv "${pkgdir}/usr/share/doc/kodi/${licensef}" \
      "${pkgdir}/usr/share/licenses/${pkgname}"
  done
  
  # SBC/embedded niceties
  install -Dm0644 ${srcdir}/kodi_permissions.conf ${pkgdir}/etc/tmpfiles.d/kodi_permissions.conf
  install -Dm0644 ${srcdir}/kodi.service ${pkgdir}/usr/lib/systemd/system/kodi.service
  install -Dm0644 ${srcdir}/polkit.rules ${pkgdir}/etc/polkit-1/rules.d/10-kodi.rules

  # fix permissions necessary for accelerated video playback
  install -Dm0644 ${srcdir}/99-odroid.rules ${pkgdir}/etc/udev/rules.d/99-odroid.rules

  # python2 is being used
  cd "${pkgdir}"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "${file}"; done
}

# kodi-c2-eventclients
# components: kodi-eventclients-common kodi-eventclients-ps3 kodi-eventclients-wiiremote kodi-eventclients-xbmc-send

package_kodi-c2-eventclients() {
  pkgdesc="Kodi Event Clients (ODROID-C2)"
  provides=('kodi-eventclients')
  conflicts=('kodi-eventclients')

  depends=('cwiid')

  _components=(
    'kodi-eventclients-common'
    'kodi-eventclients-ps3'
    'kodi-eventclients-wiiremote'
    'kodi-eventclients-xbmc-send'
  )

  cd kodi-build
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="${pkgdir}" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="${_cmp}" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "${pkgdir}"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "${file}"; done
}

# kodi-c2-tools-texturepacker
# components: kodi-tools-texturepacker

package_kodi-c2-tools-texturepacker() {
  pkgdesc="Kodi Texturepacker tool (ODROID-C2)"
  depends=('libpng' 'giflib' 'libjpeg-turbo' 'lzo')
  provides=('kodi-tools-texturepacker')

  _components=(
    'kodi-tools-texturepacker'
  )

  cd kodi-build
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="${pkgdir}" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="${_cmp}" \
      -P cmake_install.cmake
  done
}

# kodi-c2-dev
# components: kodi-addon-dev kodi-audio-dev kodi-eventclients-dev kodi-game-dev kodi-inputstream-dev kodi-peripheral-dev kodi-pvr-dev kodi-screensaver-dev kodi-visualization-dev

package_kodi-c2-dev() {
  pkgdesc="Kodi dev files (ODROID-C2)"
  depends=('kodi')
  provides=('kodi-dev')

  _components=(
    'kodi-addon-dev'
    'kodi-audio-dev'
    'kodi-eventclients-dev'
    'kodi-game-dev'
    'kodi-inputstream-dev'
    'kodi-peripheral-dev'
    'kodi-pvr-dev'
    'kodi-screensaver-dev'
    'kodi-visualization-dev'
  )

  cd kodi-build
  # install eventclients
  for _cmp in ${_components[@]}; do
    DESTDIR="${pkgdir}" /usr/bin/cmake \
      -DCMAKE_INSTALL_COMPONENT="${_cmp}" \
      -P cmake_install.cmake
  done

  # python2 is being used
  cd "${pkgdir}"
  grep -lR '#!.*python' * | while read file; do sed -s 's/\(#!.*python\)/\12/g' -i "${file}"; done
}

